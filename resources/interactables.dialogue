~ electrical_panel

if not State.electrical_panel.panel_removed:
	You remove the panel. There is a battery inside.
	set State.electrical_panel.panel_removed = true

if not State.electrical_panel.battery_taken:
	Take the battery?
	- Yes
		You took the battery.
		set State.electrical_panel.battery_taken = true
		do State.electrical_panel.remove_from_group("Interactable")
		=> END
	- No => END

=> END

~ broken_pipe

There is an exposed electrical wire here.
if State.electrical_panel.battery_taken and not State.broken_pipe.battery_charged:
	Charge the battery?
	- Yes
		The battery is now charged.
		set State.broken_pipe.battery_charged = true
		do State.broken_pipe.remove_from_group("Interactable")
	- No => END
	
=> END

~ computer_table 
if not State.computer_table.powered:
	It is a large table with a computer screen. The computer currently has no power.
	if State.electrical_panel.battery_taken:
		Insert the battery?
		- Yes
			You insert the battery.
			if not State.broken_pipe.battery_charged:
				Computer: ERROR - External power source voltage is too low!
			else:
				The computer turns on.
				set State.computer_table.powered = true
		- No => END
if State.computer_table.puzzle_1_solved:
	Computer: Welcome superuser, please select an action below.
	- View captain's log
		Now loading latest captain's log...
	- Close => END
=> END

~ red_door
if not State.computer_table.puzzle_1_solved:
	Red door: ERROR - superuser level required. The incident will be reported.
else:
	Red door: Superuser level confirmed.
	do State.red_door.erase()
	The door unlocks.
=> END

~ crowbar
do State.crowbar.sprite.queue_free()
You picked up the crowbar.
do State.crowbar.pickup()

=> END

~ grate
if not State.crowbar.taken:
	It's a metal grate. It's too heavy to move with your hands alone.
else:
	Use the crowbar?
	- Yes
		You pry the grate open.
	- No => END 
=> END